----Ports Down Network Devices
import "strings"

from(bucket: "librenms")
  |> range(start: v.timeRangeStart, stop: v.timeRangeStop)
  |> filter(fn: (r) =>
    r._measurement == "librenms_port_states" and
    r._field == "ifOperStatus" and
    r.hostname =~ /^${networkdevices:regex}$/
  )
  // Choose only the required interfaces per host
  |> filter(fn: (r) =>
    if r.hostname == "gl-ep-bxl-jan-sw01" then
      // RE0: ge-0/0/(1|2|3|4|12), xe-0/2/0
      // RE1: ge-1/0/(1|2|3|4|12), xe-1/2/0
      r.ifName =~ /^(ge-[01]\/0\/(1|2|3|4|12)|xe-[01]\/2\/0)$/
    else if r.hostname =~ /^(gl-ep-bxl-phs-hem-sw01|gl-ep-stb-hem-sw01)$/ then
      // RE0: ge-0/0/(1|2|12), xe-0/2/0
      // RE1: ge-1/0/(1|2|12), xe-1/2/0
      r.ifName =~ /^(ge-[01]\/0\/(1|2|12)|xe-[01]\/2\/0)$/
    else if r.hostname =~ /^(gl-ep-bxl-fsw01|gl-ep-stb-fsw01)$/ then
      // RE0: ge-0/2/(0|1|2|3)
      // RE1: ge-1/2/(0|1|2|3)
      r.ifName =~ /^ge-(0|1)\/2\/(0|1|2|3)$/
    else if r.hostname == "gl-ep-bxl-fw" then
      // Treat as RE0
      r.ifName =~ /^x(|1|3|4)$/
    else if r.hostname == "gl-ep-stb-fw" then
      // Treat as RE0
      r.ifName =~ /^x(|1|3)$/
    else
      false
  )
  // Latest status per port
  |> group(columns: ["hostname","ifName"])
  |> last()
  // Derive sensor_descr and map UP->1 else 0
  |> map(fn: (r) => ({
      _time: r._time,
      hostname: r.hostname,
      sensor_descr:
        if r.hostname =~ /^(gl-ep-bxl-fw|gl-ep-stb-fw)$/ then
          "Routing Engine 0"
        else if r.ifName =~ /^(ge-0\/|xe-0\/)/ then
          "Routing Engine 0"
        else if r.ifName =~ /^(ge-1\/|xe-1\/)/ then
          "Routing Engine 1"
        else
          "Routing Engine 0",
      ok: if strings.toUpper(v: string(v: r._value)) == "UP" then 1.0 else 0.0
  }))
  // Aggregate per hostname & sensor_descr: min = 1 only if all required ports are 1
  |> group(columns: ["hostname","sensor_descr"])
  |> min(column: "ok")
  |> rename(columns: {ok: "_value"})
  |> keep(columns: ["_time","hostname","sensor_descr","_value"])
  |> yield(name: "all_required_ports_up_by_RE")
